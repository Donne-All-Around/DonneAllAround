plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.sturdy'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// spring security 설정
	//implementation 'org.springframework.boot:spring-boot-starter-security'

	//Redis 설정
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	//swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

	// querydsl 설정
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// firebase fcm 설정
	implementation 'com.google.firebase:firebase-admin:9.1.1'
	implementation 'com.squareup.okhttp3:okhttp:4.11.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

// querydsl 추가 설정 (선택 사항)
def querydslDir = "$buildDir/generated/querydsl"

// java source set에 Q클래스 적용
sourceSets {
	main.java.srcDirs += [querydslDir]
}

// Q클래스 location 위치 적용
tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

// gradle clean task 실행시 Q클래스 삭제
clean {
	delete file(querydslDir)
}